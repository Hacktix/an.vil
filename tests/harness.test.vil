fn print_title(title) {
    io.println();
    io.println("========== " + title + " ==========");
    io.println();
}

fn start_tests() {
    __total_tests ??= {
        total: 0,
        failed: 0
    };
}

fn end_tests() {
    io.println("==========================================");
    io.println("# ALL TESTS COMPLETED");
    io.println("==========================================");
    io.println("# Failed " + @__total_tests.failed + " out of " + @__total_tests.total + " cases");
    io.println("==========================================");
}

fn print_section(section) {
    io.println(" > " + section);
}

fn log_case(case, result, expected) {
    __logged_test_cases ??= array(0);
    __total_tests.total++;
    arr.push(__logged_test_cases, {case: case, result: result, expected: expected});
}

fn print_cases() {
    rw val pad_target_len = 0;
    each(rw val _,c : __logged_test_cases) {
        if(pad_target_len < #c.case)
            pad_target_len = #c.case;
    }
    
    rw val passed = 0;
    each(rw val i,c : __logged_test_cases) {
        val title = str.rpad(c.case, " ", pad_target_len);
        io.print("  * " + title + " -> ");
        if(equals(c.result, c.expected)) {
            io.println("Passed!");
            passed++;
        }
        else {
            io.println("Failed! Expected " + @c.expected + " but got " + @c.result);
            __total_tests.failed++;
        }
    }
    io.println(" >> Passed " + @passed + " of " + @#__logged_test_cases + " tests.");
    io.println();
    __logged_test_cases = array(0);
}

fn equals(a,b) {
    if(typeof(a) != typeof(b)) -> false;

    if(a is Array) -> compare_array(a,b);
    ret a == b;
}

fn compare_array(a, b) {
    if(a !is Array || b !is Array) -> false;
    if(#a != #b) -> false;
    each(rw val i,v : a) {
        if(v != b[i]) -> false;
    }
    ret true;
}